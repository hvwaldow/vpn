#!/usr/bin/expect

# HvW - 2016-06-26
# Serves to establish a VPN connection that requires 2-factor
# authentification.
#
# This script is quite specific for my environment.
# The expect - code is so that it "just works for me".
#
# Assumptions:
# + expect (http://expect.sourceforge.net/) is installed.
# + VPN client installed: Cisco AnyConnect Secure Mobility Client.
# + The Standard Unix Password Manager "pass"
#   (https://www.passwordstore.org/) is installed.
# + Authentication via RFC 6238 (TOTP) as "second factor" is enabled.
# + oathtool (https://savannah.nongnu.org/projects/oath-toolkit/) is installed.
# + Both, password and TOTP-key are managed by pass.

set USERNAME "vonwalha"
set timeout 20

proc usage {} {
    puts "Usage:"
    puts "    vpn on | off \[eaw | ethz\]\r"
    puts "    vpn -h\r"
    exit
}

if {[lindex $argv 0] == "-h" || $argc == 0} {
    usage
}

set serv [lindex $argv 1]
if {$serv == "" || $serv == "eaw"} {
    set VPNSERVER "sslvpn.eawag.ch"
    set PASS_PATH_KEY "eawag/otp"
    set PASS_PATH_UN "eawag/vonwalha"
    set GROUP "Eawag"
} elseif {$serv == "ethz"} {
    set VPNSERVER "sslvpn.ethz.ch"
    set PASS_PATH_UN "eawag/nethz-pw"
    set GROUP "ETHZ"
} else {
    puts "No valid vpnserver provided"
    usage
}
	
if {[lindex $argv 0] == "on"} {
    spawn "/opt/cisco/anyconnect/bin/vpn"
    expect "VPN>" { send "connect $VPNSERVER\r" }
    if {$VPNSERVER == "sslvpn.eawag.ch"} {
	expect "*onnect Anyway?*" { send "y\r" }
	expect "*Group*" { send "$GROUP\r" }
    }
    expect "*sername:*" { send "$USERNAME\r" }
    set pw [exec pass $PASS_PATH_UN]
    expect "*assword:*" { send "$pw\r" }
    if {$VPNSERVER == "sslvpn.eawag.ch"} {
	set oatkey [exec pass $PASS_PATH_KEY]
	set code [exec oathtool  --totp $oatkey]
	expect "*PASSCODE*" { send "$code\r" }
    }
    expect "state: Connected" { exit }

} elseif {[lindex $argv 0] == "off"} {
    spawn "/opt/cisco/anyconnect/bin/vpn"
    expect "VPN>" { send "disconnect\r" }
    expect {
	"not connected" { exit }
	"Disconnected" { exit }
    }
}
